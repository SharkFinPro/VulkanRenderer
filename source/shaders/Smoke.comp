#version 450

struct Particle {
  vec4 positionTtl;
  vec3 velocity;
  vec4 color;
};

layout(binding = 0) uniform ParameterUBO {
  float deltaTime;
} ubo;

layout(set = 0, binding = 4) uniform Smoke {
  vec3 systemPosition;
  float spreadFactor;
  float maxSpreadDistance;
  float windStrength;
} smoke;

layout(std140, binding = 1) readonly buffer ParticleSSBOIn {
  Particle particlesIn[ ];
};

layout(std140, binding = 2) buffer ParticleSSBOOut {
  Particle particlesOut[ ];
};

layout (local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

const float TTL = 8.0;

vec3 applyWind(vec3 position, float ttl)
{
  vec3 windDir = vec3(sin(ttl * 0.1), 0.0, cos(ttl * 0.3));

  float turbulence = sin(position.y * 0.5 + ttl) * 0.15 +
  cos(position.x * 0.3 + ttl * 0.7) * 0.1;

  return windDir * smoke.windStrength + vec3(turbulence, 0.0, turbulence);
}

vec3 generateSpreadDirection(vec3 position, float ttl)
{
  vec3 direction = normalize(position - vec3(0.0));

  if(length(direction) < 0.01)
  {
    direction = normalize(vec3(sin(ttl * 3.14159 * 7.0), cos(ttl * 3.14159 * 11.0), sin(ttl * 3.14159 * 13.0)));
  }

  return direction;
}

vec3 calcSpreadForce(vec3 position, float ttl)
{
  float normalizedTime = ttl / TTL; // Value from 0 to 1
  float spreadStrength = min(smoke.spreadFactor * log(1 + 20 * normalizedTime), 0.5);

  vec3 spreadDirection = generateSpreadDirection(position, ttl);

  vec3 spreadForce = spreadDirection * spreadStrength;

  float distFromOrigin = length(position);
  if (distFromOrigin > smoke.maxSpreadDistance)
  {
    spreadForce -= normalize(position) * 0.05 * (distFromOrigin - smoke.maxSpreadDistance);
  }

  return spreadForce;
}

float rand(float seed)
{
  return fract(sin(seed * 91.3458) * 47453.5453);
}

float randRange(float seed, float minVal, float maxVal)
{
  return minVal + (maxVal - minVal) * rand(seed);
}

vec3 regenerateInitialPosition(float seed)
{
  float r = sqrt(rand(seed)) * 0.25;
  float theta = rand(seed + 1.0) * 6.28318530718; // 2Ï€

  float x = r * cos(theta);
  float z = r * sin(theta);

  float scaleX = randRange(seed + 2.0, -4.0, 4.0); // Match largeDistribution
  float scaleZ = randRange(seed + 3.0, -4.0, 4.0);

  return vec3(x * scaleX, 0.0, z * scaleZ) + smoke.systemPosition;
}

vec3 regenerateInitialVelocity(float seed)
{
  float vx = randRange(seed + 4.0, -0.25, 0.25);
  float vz = randRange(seed + 5.0, -0.25, 0.25);

  float vy = randRange(seed + 6.0, 0.75, 2.5);

  return vec3(vx, vy, vz);
}

void main()
{
  uint index = gl_GlobalInvocationID.x;

  Particle particle = particlesIn[index];

  vec3 position = particle.positionTtl.xyz;
  float ttl = particle.positionTtl.w + ubo.deltaTime;

  if (ttl > TTL)
  {
    float seed = (position.x + position.y + position.z) / 3.0;

    particle.velocity = regenerateInitialVelocity(seed);

    position = regenerateInitialPosition(seed);

    particle.positionTtl = vec4(position, TTL - ttl);

    particlesOut[index] = particle;

    return;
  }

  if (ttl > 0.0)
  {
    vec3 wind = applyWind(position, ttl);

    vec3 spreadForce = calcSpreadForce(position, ttl);

    particle.velocity += (wind + spreadForce) * ubo.deltaTime;

    position += particle.velocity * ubo.deltaTime;

    particle.color.a = sqrt((TTL - particle.positionTtl.w) / TTL);
  }
  else
  {
    particle.color.a = 0.0;
  }

  particle.positionTtl = vec4(position, ttl);

  particlesOut[index] = particle;
}